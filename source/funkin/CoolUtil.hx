package funkin;

import funkin.data.BaseSong;
import haxe.io.Bytes;
import haxe.io.Path;

import math.CoolMath;

import flixel.math.FlxPoint;
import flixel.util.FlxColor;
import flixel.tweens.FlxEase;
import flixel.util.typeLimit.OneOfTwo;

#if sys
import sys.io.File;
import sys.FileSystem;
#end
#if linc_filedialogs
import filedialogs.FileDialogs;
#else
import lime.ui.FileDialog;
import openfl.net.FileFilter;
#end

using StringTools;

/**
	Class for various util functions.  
	Math function shall go to `CoolMath`
**/
class CoolUtil {
	public static function makeSound(asset:flixel.system.FlxAssets.FlxSoundAsset) {
		var snd = new FlxSound();
		snd.loadEmbedded(asset);
		snd.exists = true;
		FlxG.sound.list.add(snd);
		return snd;
	}

	// TODO: reuse song instances n get rid of this
	public static function indexOfSong(songList:Array<BaseSong>, song:BaseSong):Int {
		// return songList.indexOf(song);
		if (song != null) for (i => s in songList) {
			if (song.songId == s.songId && song.folder == s.folder) {
				return i;
			}
		}
		return -1;
	}

	// TIRED OF WRITING THIS FUCKING SHIT
	public static function updateIndex(curIdx:Int, val:Int, length:Int) {
		curIdx += val;

		if (curIdx < 0)
			curIdx += length;
		else if (curIdx >= length)
			curIdx %= length;

		return curIdx;
	}

	public static function updateDifficultyIndex(curDiffIdx:Int, curDiffId:String, newDiffIds:Array<String>) {
		var idx = newDiffIds.indexOf(curDiffId);
		if (idx != -1)
			return idx;

		idx = newDiffIds.indexOf("normal");
		if (idx != -1)
			return idx;

		idx = newDiffIds.indexOf("hard");
		if (idx != -1)
			return idx;
		
		return curDiffIdx < 0 ? 0 : curDiffIdx;
	}

	public static function prettyInteger(num:Int):String {
		var buf = new StringBuf();

		if (num < 0) {
			num = -num;
			buf.add('-');
		}

		var str = Std.string(num);
		var h = str.length - 1;
		var i = 1;

		buf.add(str.charAt(0));
		while (i < str.length) {
			if (h % 3 == 0) buf.add(',');
			buf.add(str.charAt(i));
			h--; i++;
		}

		return buf.toString();
	}

	public static function structureToMap(st:Dynamic):Map<String, Dynamic> {
		return [
			for (k in Reflect.fields(st)){
				k => Reflect.field(st, k);
			}
		];
	}

	public static function alphabeticalSort(a:String, b:String):Int {
		// https://haxe.motion-twin.narkive.com/BxeZgKeh/sort-an-array-string-alphabetically
		a = a.toLowerCase();
		b = b.toLowerCase();
		if (a < b) return -1;
		if (a > b) return 1;
		return 0;	
	}

	public static function stringSort(ordering:Array<String>, a:String, b:String):Int {
		if(a==b) return 0;

		var aHasDefault = ordering.contains(a);
		var bHasDefault = ordering.contains(b);
		if (aHasDefault && bHasDefault)
			return ordering.indexOf(a) - ordering.indexOf(b);
		else if(aHasDefault)
			return 1;
		else if(bHasDefault)
			return -1;
		else
			return alphabeticalSort(a, b);
	}

	public static function customSort<T>(ordering:Array<T>, a:T, b:T):Int {
		if(a==b) return 0;

		var aHasDefault = ordering.contains(a);
		var bHasDefault = ordering.contains(b);
		if (aHasDefault && bHasDefault)
			return ordering.indexOf(a) - ordering.indexOf(b);
		else if(aHasDefault)
			return 1;
		else if(bHasDefault)
			return -1;
		else
			return 0;
	}

	////
	inline public static function blankSprite(width, height, color=0xFFFFFFFF) {
		var spr = new FlxSprite().makeGraphic(1, 1);
		spr.scale.set(width, height);
		spr.updateHitbox();
		spr.color = color;
		return spr;
	}
	
	public static function makeOutlinedGraphic(Width:Int, Height:Int, Color:Int, LineThickness:Int, OutlineColor:Int)
	{
		var rectangle = flixel.graphics.FlxGraphic.fromRectangle(Width, Height, OutlineColor, true);
		rectangle.bitmap.fillRect(
			new openfl.geom.Rectangle(
				LineThickness, 
				LineThickness, 
				Width-LineThickness*2, 
				Height-LineThickness*2
			),
			Color
		);

		return rectangle;
	};

	/**
	 * @param spr The sprite on which to clone the animation
	 * @param ogName Name of the animation to be cloned. 
	 * @param cloneName Name of the resulting clone.
	 * @param force Whether to override the resulting animation, if it exists.
	 */
	public static function cloneSpriteAnimation(spr:FlxSprite, ogName:String, cloneName:String, force:Bool=false)
	{
		var daAnim = spr.animation.getByName(ogName);
		if (daAnim!=null && (force==true || !spr.animation.exists(cloneName)))
			spr.animation.add(cloneName, daAnim.frames, daAnim.frameRate, daAnim.looped, daAnim.flipX, daAnim.flipY);
	}

	@:noCompletion static var _point:FlxPoint = new FlxPoint();
	public static function overlapsMouse(object:FlxObject, ?camera:FlxCamera):Bool
	{
		camera ??= FlxG.camera;

		FlxG.mouse.getPositionInCameraView(camera, _point);
		if (camera.containsPoint(_point)) {
			FlxG.mouse.getWorldPosition(camera, _point);
			return object.overlapsPoint(_point, true, camera);
		}

		return false;
	}

	public static function centerOnObject(obj1:FlxObject, obj2:FlxObject) {
		obj1.x = obj2.x + (obj2.width - obj1.width) / 2;
		obj1.y = obj2.y + (obj2.height - obj2.height) / 2;
		return obj1;
	}

	////
	public static function listFromString(string:String):Array<String>
	{
		string = string.trim();
		if (string.length == 0)
			return [];

		var daList:Array<String> = string.split('\n');
		for (i in 0...daList.length)
			daList[i] = daList[i].trim();
		
		return daList;
	}
	public static function coolTextFile(path:String):Array<String>
	{
		var rawList = Paths.getContent(path);
		if (rawList == null)
			return [];

		return listFromString(rawList);
	}

	public static function dominantColor(sprite:flixel.FlxSprite):FlxColor {
		final alphaTolerance:Int = 10; // 0 - 255

		var colorCount:Map<Int, Int> = [];
		for (col in 0...sprite.frameWidth) {
			for (row in 0...sprite.frameHeight) {
				var pixelColor:FlxColor = sprite.pixels.getPixel32(col, row);
				if (pixelColor.alpha >= alphaTolerance) {
					pixelColor.alpha = 255;	
					colorCount[pixelColor] = colorCount.exists(pixelColor) ? colorCount[pixelColor] + 1 : 1;
				}
			}
		}
		colorCount[0xFF000000] = 0;

		var maxCount:Int = 0;
		var maxKey:Int = 0; // most occurring color
		for (key in colorCount.keys()) {
			if (colorCount[key] >= maxCount) {
				maxCount = colorCount[key];
				maxKey = key;
			}
		}

		colorCount.clear();
		return maxKey;
	}

	////
	public static function colorFromString(color:String):FlxColor
	{
		return FlxColor.fromRGB(
			Std.parseInt("0x"+color.substr(-6, 2)),
			Std.parseInt("0x"+color.substr(-4, 2)),
			Std.parseInt("0x"+color.substr(-2, 2)),
			Std.parseInt("0x"+color.substr(-8, 2))
		);
	}

	// could probably use a macro
	public static function getEaseFromString(?name:String):EaseFunction
	{
		return switch(name)
		{
 			case "backIn": FlxEase.backIn;
 			case "backInOut": FlxEase.backInOut;
 			case "backOut": FlxEase.backOut;
 			case "bounceIn": FlxEase.bounceIn;
 			case "bounceInOut": FlxEase.bounceInOut;
 			case "bounceOut": FlxEase.bounceOut;
 			case "circIn": FlxEase.circIn;
 			case "circInOut": FlxEase.circInOut;
 			case "circOut": FlxEase.circOut;
 			case "cubeIn": FlxEase.cubeIn;
 			case "cubeInOut": FlxEase.cubeInOut;
 			case "cubeOut": FlxEase.cubeOut;
 			case "elasticIn": FlxEase.elasticIn;
 			case "elasticInOut": FlxEase.elasticInOut;
 			case "elasticOut": FlxEase.elasticOut;
 			case "expoIn": FlxEase.expoIn;
 			case "expoInOut": FlxEase.expoInOut;
 			case "expoOut": FlxEase.expoOut;
 			case "quadIn": FlxEase.quadIn;
 			case "quadInOut": FlxEase.quadInOut;
 			case "quadOut": FlxEase.quadOut;
 			case "quartIn": FlxEase.quartIn;
 			case "quartInOut": FlxEase.quartInOut;
 			case "quartOut": FlxEase.quartOut;
 			case "quintIn": FlxEase.quintIn;
 			case "quintInOut": FlxEase.quintInOut;
 			case "quintOut": FlxEase.quintOut;
 			case "sineIn": FlxEase.sineIn;
 			case "sineInOut": FlxEase.sineInOut;
 			case "sineOut": FlxEase.sineOut;
 			case "smoothStepIn": FlxEase.smoothStepIn;
 			case "smoothStepInOut": FlxEase.smoothStepInOut;
 			case "smoothStepOut": FlxEase.smoothStepOut;
 			case "smootherStepIn": FlxEase.smootherStepIn;
 			case "smootherStepInOut": FlxEase.smootherStepInOut;
 			case "smootherStepOut": FlxEase.smootherStepOut;

 			case "instant": (t:Float) -> return 1.0;
			default: FlxEase.linear;
		}
	}

	inline public static function numberArray(max:Int, ?min = 0):Array<Int>
	{
		// max+1 because in haxe for loops stop before reaching the max number
		return [for (n in min...max+1){n;}];
	}

	//uhhhh does this even work at all? i'm starting to doubt
	public static function precacheSound(sound:String, ?library:String = null):Void {
		Paths.sound(sound, library);
	}

	public static function precacheMusic(sound:String, ?library:String = null):Void {
		Paths.music(sound, library);
	}

	public static inline function browserLoad(site:String) {
		flixel.FlxG.openURL(site);
	}

	public static function safeSaveFile(path:String, content:OneOfTwo<String, Bytes>):Bool {
		#if sys
		try {
			FileSystem.createDirectory(Path.directory(path));
			if (content is Bytes)
				File.saveBytes(path, content);
			else
				File.saveContent(path, content);
			return true;
		}
		catch(e) {
			final errMsg:String = 'Error while trying to save the file: ${Std.string(e).replace('\n', ' ')}';
			trace(errMsg);
		}
		#end

		return false;
	}

	@:noCompletion
	private static inline function _filefilters(?filters:Array<String>) {
		#if linc_filedialogs
		return filters ?? [];
		#else		
		final goodFilters:Array<String> = [];
		if (filters != null) {
			for (f in filters) {
				var type = new FileFilter(f, f);
				goodFilters.push(StringTools.replace(StringTools.replace(type.extension, "*.", ""), ";", ","));
			}
		}
		return goodFilters.join(";");
		#end
	}

	/**
		Normalize a path to be used by the the file system.

		On Windows, slashes `/` are replaced by backslashes `\`
		
		If `path` is `null`, or if the resulting path doesn't exist, the current working directory is returned.

		@param path File path, can be relative or absolute.
		@return An absolute path to be used in system functions.
	**/
	public static inline function getSystemPath(?path:String):String {
		#if sys
		if (path == null || path.length == 0)
			return Sys.getCwd();
		if (!Path.isAbsolute(path))
			path = Path.normalize(Path.addTrailingSlash(Sys.getCwd()) + path);
		
		if (!FileSystem.exists(Path.directory(path)))
			path = Sys.getCwd();
		#if windows else
			path = path.replace('/', '\\');
		#end

		return path;
		#else
		return "";
		#end
	}

	public static function showOpenMultipleDialog(title:String = "Open Files", ?defaultPath:String, ?filters:Array<String>, ?onSelect:(paths:Array<String>)->Void, ?onCancel:Void->Void):Void {
		final filters = _filefilters(filters);
		final defaultPath = getSystemPath(defaultPath);
		#if linc_filedialogs
		final files:Array<String> = FileDialogs.open_file(title, cast defaultPath, cast filters, Option.Multiselect);
		if (files.length == 0) {
			if (onCancel != null) onCancel();
		}else {
			if (onSelect != null) onSelect(files);
		}
		#else
		final dialog:FileDialog = new FileDialog();
		if (onCancel != null) dialog.onCancel.add(onCancel);
		if (onSelect != null) dialog.onSelectMultiple.add(onSelect);
		dialog.browse(OPEN_MULTIPLE, filters, defaultPath, title);
		Sys.sleep(0.5); // sleep to prevent dialogs sometimes not opening if opened in quick succession
		#end
	}
	
	public static function showOpenDialog(title:String = "Open File", ?defaultPath:String, ?filters:Array<String>, ?onOpen:(bytes:Bytes)->Void, ?onSelect:(path:String)->Void, ?onCancel:Void->Void):Void {
		final filters = _filefilters(filters);
		final defaultPath = getSystemPath(defaultPath);
		#if linc_filedialogs
		final files:Array<String> = FileDialogs.open_file(title, cast defaultPath, cast filters, Option.None);
		if (onSelect != null) onSelect(files[0]);
		if (files.length == 0) {
			if (onCancel != null) onCancel();
		}else {
			if (onOpen != null) onOpen(File.getBytes(files[0]));
		}
		#else
		final dialog:FileDialog = new FileDialog();
		if (onOpen != null) dialog.onOpen.add(onOpen);
		if (onCancel != null) dialog.onCancel.add(onCancel);
		if (onSelect != null) dialog.onSelect.add(onSelect);
		dialog.browse(OPEN, filters, defaultPath, title);
		Sys.sleep(0.5); // sleep to prevent dialogs sometimes not opening if opened in quick succession
		#end
	}

	public static function showSaveDialog(content:OneOfTwo<String, Bytes>, title:String = "Save File", ?defaultPath:String, ?filters:Array<String>, ?onSave:(path:String)->Void, ?onCancel:Void->Void):Void {
		final filters = _filefilters(filters);
		final defaultPath = getSystemPath(defaultPath);
		#if linc_filedialogs
		final savePath:String = FileDialogs.save_file(title, cast defaultPath, cast filters);
		if (savePath.length == 0) {
			if (onCancel != null) onCancel();
		}else {
			var success:Bool = safeSaveFile(savePath, content);
			if (success && onSave != null) onSave(savePath);
		}
		#else
		final dialog:FileDialog = new FileDialog();
		dialog.onSelect.add((f) -> safeSaveFile(f, content));
		if (onCancel != null) dialog.onCancel.add(onCancel);
		if (onSave != null) dialog.onSelect.add(onSave);
		dialog.browse(SAVE, filters, defaultPath, title);
		Sys.sleep(0.5); // sleep to prevent dialogs sometimes not opening if opened in quick succession
		#end
	}

	// https://community.haxe.org/t/clone-a-class-instance/3747/5
	// shoutout random guy on haxe fourm
    public static function copyClass<T>(c:T):T {
        var cls:Class<T> = Type.getClass(c);
        var inst:T = Type.createEmptyInstance(cls);
        var fields = Type.getInstanceFields(cls);

        for (field in fields) {
            var val:Dynamic = Reflect.field(c,field);
            if (!Reflect.isFunction(val)) {
                if (val is Array) {
                    Reflect.setField(inst,field,val.copy()); // WHAT THE FUCK HAXE??
                }
                else{
                    Reflect.setField(inst,field,val);
                }
            }
        }
        return inst;
    }


	////
	@:noCompletion inline public static function coolLerp(current:Float, target:Float, elapsed:Float):Float
		return CoolMath.coolLerp(current, target, elapsed);

	@:noCompletion inline public static function scale(x:Float, lower1:Float, higher1:Float, lower2:Float, higher2:Float):Float
		return CoolMath.scale(x, lower1, higher1, lower2, higher2);

	@:noCompletion inline public static function quantizeAlpha(f:Float, interval:Float):Float
		return CoolMath.quantizeAlpha(f, interval);

	@:noCompletion inline public static function quantize(f:Float, snap:Float):Float
		return CoolMath.quantize(f, snap);

	@:noCompletion inline public static function snap(f:Float, snap:Float):Float
		return CoolMath.snap(f, snap);

	@:noCompletion inline public static function boundTo(value:Float, min:Float, max:Float):Float
		return CoolMath.boundTo(value, min, max);

	@:noCompletion inline public static function clamp(n:Float, lower:Float, higher:Float):Float
		return CoolMath.clamp(n, lower, higher);

	@:noCompletion inline public static function floorDecimal(value:Float, decimals:Int):Float
		return CoolMath.floorDecimal(value, decimals);

	@:noCompletion inline public static function rotate(x:Float, y:Float, rads:Float, ?point:FlxPoint):FlxPoint
		return CoolMath.rotate(x, y, rads, point);
}
